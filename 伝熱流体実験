1000 'SAVE "CONDIS"
1010 '**************************************************************
1020 '**      一次定常熱伝導　　　　　　　　　　　　　    　      **
1030 '**************************************************************
1040 '    T19T079  本田龍之祐
1050 '******* 配列の宣言 *******************************************
1060  NN=41
1070  DIM T(NN), Q(NN), X(NN)  '温度、発熱分布、評価点のX座標
1080  DIM A(NN), B(NN), C(NN), D(NN) '三重対角行列の係数
1090 '*********************　計算条件 ******************************
1100  LX=1                     '計算領域長さ　[m]
1110  NI=20                    'セル数(=評価点数)
1120  RAMD=1                   '熱伝導率　[W/mK]
1130  BC=3                     'BC=1:ディリクエ型、=2:ノイマン型
1140  IF BC=1 THEN TB1=1 : TB2=0 'TB1:左境界温度、TB2:右境界温度
1150  IF BC=2 THEN QB1=0 : QB2=0 'QB1:左境界熱流束、QB2:右境界熱流束
1151  IF BC=3 THEN TB1=0 : QB2=1
1160  FOR I=1 TO NI : Q(1)=6 : Q(2)=6 : Q(3)=6 : Q(4)=2 : Q(5)=6 :
1161   Q(6)=6 : Q(7)=2 : Q(8)=1 : Q(9)=1 : Q(10)=9 : Q(11)=6 : 
1162   Q(12)=6 : Q(13)=6 : Q(14)=2: Q(15)=6 : Q(16)=6 : Q(17)=2 :
1163   Q(18)=1 : Q(19)=1 : Q(20)=9 : NEXT '発熱分布 [W/m?]
1170 '********************* 定数計算 ********************************
1180  DX=LX/NI                 ' セルの幅　[m]
1190  GOSUB *INITGRA1          ' グラフィックスの設定
1200 '************* 計算本体 ****************************************
1210 '         **** 三重対角行列の係数設定 ******************
1220  FOR I=1 TO NI
1230    A(I)=-RAMD/DX/DX : B(I)=2*RAMD/DX/DX : C(I)=-RAMD/DX/DX
1240    D(I)=Q(I)
1250  NEXT
1260  IF BC=1 THEN B(0)=.5 :  C(0) =.5 : D(0)=TB1 '*** 境界条件(始)
1270  IF BC=2 THEN B(0)=-1/DX : C(0)=1/DX : D(0)=QB1/RAMD
1280  IF BC=1 THEN A(NI+1)=.5 : B(NI+1)=.5 : C(NI+1)=0
1290  IF BC=1 THEN D(NI+1)=TB2
1300  IF BC=2 THEN A(NI+1)=1/DX : B(NI+1)=-1/DX : C(NI+1)=0
1310  IF BC=2 THEN D(NI+1)=QB2/RAMD
1311  IF BC=3 THEN B(0)=.5 : C(0)=.5 : D(0)=TB1
1302  IF BC=3 THEN A(NI+1)=1/DX : B(NI+1)=-1/DX : C(NI+1)=0
1313  IF BC=3 THEN D(NI+1)=QB2/RAMD     '*** 境界条件(終)
1320 '      ******** 三重対角行列を解く   ******
1330  C(0)=C(0)/B(0) : D(0)=D(0)/B(0)
1340  FOR I=1 TO NI+1 : B(I)=B(I)-A(I)*C(I-1)
1350                    C(I)=C(I)/B(I)
1360                    D(I)=(D(I)-A(I)*D(I-1))/B(I) : NEXT
1370  FOR I=NI TO 0 STEP -1 : D(I)=D(I)-C(I)*D(I+1) : NEXT
1380  FOR I=0 TO NI+1 : T(I)=D(I) : NEXT
1390  TMAX=2 : GOSUB *GRA1      '*** 結果の表示
1400  STOP
1410 '***************************************************************
1420 '**           サブルーチン                                    **
1430 '***************************************************************
1440 *INITGRA1 '******* グラフィックスの設定   **********************
1450  CONSOLE ,, 0 : SCREEN 3 : CLS 3
1460  FOR I=1 TO NI : X(I)=(I-.5) *DX : NEXT        '*** 評価点の位置
1470  X(0)=0 : X(NI+1)=LX                           '*** 両境界の位置
1480  DEF FNGX(X)=100+400*X/LX       '*** 画面上の座標を与える関数
1490  DEF FNGY(T)=350-200*T/TMAX
1500  RETURN
1510 '
1520 *GRA1 '********* グラフを描く(1次元温度分布) ********************
1530  LINE (FNGX(0), FNGY(TMAX))-(FNGX(0), FNGY(O))  '*** 座標軸
1540  LINE -(FNGX(LX), FNGY(0))
1550  LOCATE 10, 13 : PRINT "T" : LOCATE 40, 23 : PRINT "X"
1560  LOCATE 0, 0
1570  T(0)=(T(0)+T(1))/2 : T(NI+1)=(T(NI)+T(NI+1))/2 '*** 境界温度
1580  IR0=2 : PSET (FNGX(X(0)), FNGY(T(0))), IR0   '*** 分布を描く
1590  FOR I=1 TO NI+1 : LINE-(FNGX(X(I)), FNGY(T(I))), IR0 : NEXT
1600  RETURN


